syntax logo_syntax (logo_lexique) {

rule <start_symbol> {
  #-- Définition des routines
  $PROGRAM$
  @routineMap tableRoutines = {}
  repeat
  while
    <routine_definition> !?tableRoutines
  end
  #--- Programme principal
  $BEGIN$
    @instructionList instructions = {}
    <instruction_list> !?tableRoutines !?instructions
  $END$
  $.$

  # Execution
  @bool penDown = false
  @double x = 0.0
  @double y = 0.0
  @double angle = 0.0 # Angle en degrés
  @string SVG = ""
  for i in instructions do
    [i.mInstruction codeDisplay !?penDown !?x !?y !?angle !?SVG]
  end
  # SVG file output
  let @string sourceFilePath = @string.stringWithSourceFilePath
  let @string code = [filewrapper generationTemplate.svg
    ![sourceFilePath lastPathComponent]
    !SVG
  ]
  [code writeToFile
    ![sourceFilePath stringByDeletingPathExtension] + ".svg"
  ]
}

rule <routine_definition> ?!@routineMap ioTableRoutines {
  $ROUTINE$
  $identifier$ ?let @lstring routineName
  $BEGIN$
    @instructionList instructions = {}
    <instruction_list> !?ioTableRoutines !?instructions
    [!?ioTableRoutines insertKey !routineName !instructions]
  $END$
}

rule <instruction_list> ?!@routineMap ioTableRoutines ?!@instructionList instructions{
  repeat
  while
    <instruction> !?ioTableRoutines !?instructions
    $;$
  end
}

rule <instruction> ?!@routineMap ioTableRoutines ?!@instructionList instructions{
  select
    $CALL$ $identifier$ ?let @lstring routineName
    @instructionList instructionsRoutine
    [ioTableRoutines searchKey !routineName ?instructionsRoutine]
    for i in instructionsRoutine do
      instructions += !i.mInstruction
    end
  or
    $FORWARD$ $integer$ ?let @luint distance
    @instruction instruction = @forward.new {!distance}
    instructions += !instruction
  or
    $ROTATE$ $integer$ ?let @luint angle
    @instruction instruction = @rotate.new {!angle}
    instructions += !instruction
  or
    <pen_instruction> !?instructions
  or
    $REPEAT$ $integer$ ?let @luint nbrTimes
      @instructionList instructionsRepeat = {}
      <instruction_list> !?ioTableRoutines !?instructionsRepeat
    $END$
    @uint i = [nbrTimes uint]
    loop ([nbrTimes uint])
    while i > 0 do
      for inst in instructionsRepeat do
        instructions += !inst.mInstruction
      end
      i--
    end
  end
}

rule <pen_instruction> ?!@instructionList instructions{
  @instruction instruction
  $PEN$
  select
    $UP$
    instruction = @penUp.new
  or
    $DOWN$
    instruction = @penDown.new
  end
  instructions += !instruction
}

}
